#!/usr/bin/env groovy
/*
 * Build script for gradle-base-plugins
 * Copyright Â© 2017-2018  Basil Peace
 *
 * This file is part of gradle-base-plugins.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */
plugins {
  id 'org.fidata.project.java'
  id 'org.fidata.project.groovy'
  id 'org.fidata.plugin'
}

description = 'Base plugins for Gradle projects and plugins developed by FIDATA'
tags.set(['defaults', 'opinions', 'opinionated', 'conventions', 'internal'])
license = 'Apache-2.0'
publicReleases = false
contacts {
  'grv87@yandex.ru' {
    moniker 'Basil Peace'
    github 'grv87'
    roles 'owner', 'developer'
  }
}

ext.rootDir = '.'

apply from: new File(ext.rootDir, 'gradle/dependencies-compile.gradle')

sourceSets.main.groovy.srcDirs += [new File(ext.rootDir, 'src/plugin-dependencies/groovy')]

/*
 * WORKAROUND:
 * https://github.com/DanySK/javadoc.io-linker/issues/5
 * `org.danilopianini.javadoc.io-linker` plugin doesn't work with `java-library` plugin
 * <grv87 2018-06-23>
 */
afterEvaluate {
  jvm.javadocLinks['org.spdx'] = uri("https://static.javadoc.io/org.spdx/spdx-tools/${ configurations.getByName('compileClasspath').resolvedConfiguration.firstLevelModuleDependencies.find { it.moduleGroup == 'org.spdx' && it.moduleName == 'spdx-tools' }.moduleVersion }/")
}

apply from: new File(ext.rootDir, 'gradle/gradle-plugin-markers.gradle')

dependencies {
  testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: 'latest.release'
}

stutter {
  java(8) {
    compatibleRange '4.9'
  }
  java(9) {
    compatibleRange '4.9'
  }
}

tasks.named('codenarcMain').configure { disabledRules.add 'JavaIoPackageAccess' }
tasks.named('codenarcCompatTest').configure { disabledRules.add 'JavaIoPackageAccess' }
tasks.named('codenarcBuildSrc').configure { disabledRules.add 'JavaIoPackageAccess' }

tasks.register('codenarcMainResources', CodeNarc) {
  for (File srcDir in sourceSets.main.resources.srcDirs) {
    source fileTree(dir: srcDir, includes: ['**/*.groovy'])
  }
  /*
   * WORKAROUND:
   * Indentation rule doesn't work correctly.
   * https://github.com/CodeNarc/CodeNarc/issues/310
   * <grv87 2018-06-26>
   */
  disabledRules.add 'Indentation'
}
